package Projectile
	import Entity
	import Fx
	import Terrain
	import Physics
	
	public enum Movement
		FREE
		SPIN
		HOMING
	
	public abstract class Projectile extends DynamicEntity
		Fx fx
		
		Movement movement = Movement.FREE
		
		real time = 0.
		boolean timed = false
		
		vec3 spinCenter
		real spinDistance = 0.
		real spinSpeedGround = 0.
		real groundAngle = 0.
		real spinSpeedAir = 0.
		real airAngle = 0.
		
		construct(vec3 pos, real radius, real angle, string sfxpath )
			super(pos, radius)	
			
			fx = new Fx(pos, angle, sfxpath)
			
		override function update()
			super.update()
			if timed
				time -= ANIMATION_PERIOD
				if time <= 0
					onTimeout()
			switch movement
				case Movement.FREE
					position += velocity
				case Movement.SPIN
					position = polarProjection3d(spinCenter, spinDistance, groundAngle, airAngle)
					groundAngle += spinSpeedGround
					airAngle += spinSpeedAir
				case Movement.HOMING
					skip
					
				
			fx.setX(position.x)
			fx.setY(position.y)
			fx.setZ(position.z - getTerrainZ(position.x, position.y) )
			
		abstract function onTimeout()
			
		function setTimed( real timeout )
			timed = true
			time = timeout
			
		function setAngleD( real angle )
			groundAngle = angle * DEGTORAD
			
		function setAngleR( real angle )
			groundAngle = angle
			
		function setSpeed( real speed )
			setForce(Cos(groundAngle)*speed, Sin(groundAngle)*speed, Sin(airAngle)*speed)
			
		function setSpinning( vec3 spinTarget, real dist, real groundSpinSpeed, real angleGround, real airSpinSpeed, real angleAir )
			movement = Movement.SPIN
			spinDistance = dist
			spinCenter = spinTarget
			spinSpeedGround = groundSpinSpeed
			spinSpeedAir = airSpinSpeed
			groundAngle = angleGround
			airAngle = angleAir
			
		function setTarget( real x, real y, real speed, real angle )
			let t = distanceBetweenCoords(position.x, position.y, x, y)/speed
			let e = getTerrainZ(x,y)
			
			let startZVelocity = ((gravity.z * t) / 2 - position.z/t + e/t)
			
			this.setForce( Cos(angle) * speed, Sin(angle) * speed, startZVelocity )
			
		ondestroy
			destroy fx
		
endpackage
